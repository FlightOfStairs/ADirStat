buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.14.4'
        classpath 'me.tatarka:gradle-retrolambda:2.4.1'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.14.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'robolectric'
apply plugin: 'retrolambda'

repositories {
    jcenter()
}

android {
    compileSdkVersion 21
    buildToolsVersion "19.1.0"

    defaultConfig {
        applicationId "org.flightofstairs.adirstat"
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'InvalidPackage' // Lombok references java.awt.
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.google.code.findbugs:jsr305:2.0.2' // ProGuard would otherwise warn about missing guava deps.
    provided 'org.projectlombok:lombok:1.14.8'
    compile 'org.roboguice:roboguice:3.0.1'
    provided 'org.roboguice:roboblender:3.0.1'

    androidTestCompile 'org.robolectric:robolectric:2.4'
    androidTestCompile 'junit:junit:4.11'
}

robolectric {
    include '**/*Test.class'
    exclude '**/espresso/**/*.class'

    ignoreFailures false

    afterTest { descriptor, result ->
        println "Executing test for ${descriptor.name} with result: ${result.resultType}"
    }
}